include "!fb6/stdlib";
include "!fb6/array";
include "!fb6/str";
include "!fb6/obj";
include "!fb6/prop";
include "!fb6/io";
include "!fb6/argparse";


func verify(override, msg) {
    if (override) {
        return 1;
    }
    tell(cat("Are you sure you want to ", msg, "?"));
    if (!str_cmpi(str_cut(io_read(), 1)[0], "y")) {
        return 1;
    }
    tell("Cancelled.");
    return 0;
}


func handle_mode_list(obj, prop) {
    var lines = prop_list_get(obj, prop);
    for (var i => var line in lines) {
        tell(fmtstring("%3i: %s", i+1, line));
    }
    tell("Done.");
}


func handle_mode_append(obj, prop, val, force) {
    if (!val) {
        argparse_show_usage();
        return;
    }
    if (verify(force, "append a line to the list")) {
        var lines = prop_list_get(obj, prop);
        lines[] = val;
        prop_list_put(obj, prop, lines);
        tell("Line appended.");
        handle_mode_list(obj, prop);
    }
}


func handle_mode_delete(obj, prop, pos, force) {
    pos = str_to_int(pos);
    if (!pos) {
        argparse_show_usage();
        return;
    }
    if (verify(force, "delete a line from the list")) {
        var lines = prop_list_get(obj, prop);
        lines = array_delitem(lines, pos-1);
        prop_list_put(obj, prop, lines);
        tell("Line deleted.");
        handle_mode_list(obj, prop);
    }
}


func handle_mode_insert(obj, prop, pos, val, force) {
    pos = str_to_int(pos);
    if (!pos || !val) {
        argparse_show_usage();
        return;
    }
    if (verify(force, "insert a line into the list")) {
        var lines = prop_list_get(obj, prop);
        lines = array_insertitem(val, lines, pos-1);
        prop_list_put(obj, prop, lines);
        tell("Line inserted.");
        handle_mode_list(obj, prop);
    }
}


func handle_mode_replace(obj, prop, pos, val, force) {
    pos = str_to_int(pos);
    if (!pos || !val) {
        argparse_show_usage();
        return;
    }
    if (verify(force, "replace a line in the list")) {
        var lines = prop_list_get(obj, prop);
        lines = array_delitem(lines, pos-1);
        lines = array_insertitem(val, lines, pos-1);
        prop_list_put(obj, prop, lines);
        tell("Line inserted.");
        handle_mode_list(obj, prop);
    }
}


func main(arg) {
    argparse_init();

    argparse_set_mode("list");
    argparse_add_mode("list",    [],        "obj=prop");
    argparse_add_mode("append",  ["force"], "obj=prop:val");
    argparse_add_mode("delete",  ["force"], "obj=prop:pos");
    argparse_add_mode("insert",  ["force"], "obj=prop:pos:val");
    argparse_add_mode("replace", ["force"], "obj=prop:pos:val");
    argparse_add_flag("verbose");

    var opts = argparse_parse(arg);
    if (!opts) return;

    if (!opts.obj || !opts.prop) {
        argparse_show_usage();
        return;
    }

    var obj = match_controlled(opts.obj);
    if (obj < 0) return;

    if (opts.verbose) {
        tell(cat("Mode = ", opts.mode));
    }
    switch (opts.mode using str_cmpi) {
        case("list")    { handle_mode_list(obj, opts.prop); }
        case("append")  { handle_mode_append(obj, opts.prop, opts.val, opts.force); }
        case("delete")  { handle_mode_delete(obj, opts.prop, opts.pos, opts.force); }
        case("insert")  { handle_mode_insert(obj, opts.prop, opts.pos, opts.val, opts.force); }
        case("replace") { handle_mode_replace(obj, opts.prop, opts.pos, opts.val, opts.force); }
    }
}


