include "!fb6/stdlib";
include "!fb6/array";
include "!fb6/str";
include "!fb6/obj";
include "!fb6/prop";
include "!fb6/io";
include "!fb6/argparse";


func verify(override, msg) {
    if (override) {
        return 1;
    }
    tell(cat("Are you sure you want to ", msg, "?"));
    if (!str_cmpi(str_cut(io_read(), 1)[0], "y")) {
        return 1;
    }
    tell("Cancelled.");
    return 0;
}


func handle_mode_list(obj, list) {
    var lines = prop_list_get(obj, list);
    for (var i => var line in lines) {
        tell(fmtstring("%i: %s", i+1, line));
    }
}


func handle_mode_append(obj, list, line) {
    var lines = prop_list_get(obj, list);
    lines[] = line;
    prop_list_put(obj, list, lines);
    tell("Line appended.");
}


func handle_mode_del(obj, list, pos) {
    var lines = prop_list_get(obj, list);
    lines = array_delitem(lines, pos-1);
    prop_list_put(obj, list, lines);
    tell("Line deleted.");
}


func handle_mode_insert(obj, list, pos, val) {
    var lines = prop_list_get(obj, list);
    lines = array_insertitem(val, lines, pos-1);
    prop_list_put(obj, list, lines);
    tell("Line inserted.");
}


func main(arg) {
    argparse_init();

    argparse_set_mode("list");
    argparse_add_mode("list",   [],        "obj=prop");
    argparse_add_mode("append", ["force"], "obj=prop:val");
    argparse_add_mode("del",    ["force"], "obj=prop:pos");
    argparse_add_mode("insert", ["force"], "obj=prop:pos:val");
    argparse_add_flag("verbose");

    if (!var opts = argparse_parse(arg)) {
        return;
    }

    if (!opts.obj || !opts.prop) {
        return argparse_show_usage();
    }

    opts.obj = obj_match(opts.obj);
    switch (opts.obj using obj_cmp) {
        case(#-1) {
            return tell("I don't see that here!");
        }
        case(#-2) {
            return tell("I don't know which object you mean!");
        }
        case(#-3) {
            opts.obj = obj_getlinks(me)[0];
        }
    }

    if (opts.verbose) {
        tell(cat("Mode = ", opts.mode));
    }
    switch (opts.mode using str_cmpi) {
        case("list") {
            handle_mode_list(opts.obj, opts.prop);
        }
        case("append") {
            if (!opts.val) {
                return argparse_show_usage();
            }
            if (verify(opts.force, "append a line to the list")) {
                handle_mode_append(opts.obj, opts.prop, opts.val);
            }
        }
        case("del") {
            opts.pos = str_to_int(opts.pos);
            if (!opts.pos) {
                return argparse_show_usage();
            }
            if (verify(opts.force, "delete a line from the list")) {
                handle_mode_del(opts.obj, opts.prop, opts.pos);
            }
        }
        case("insert") {
            opts.pos = str_to_int(opts.pos);
            if (!opts.pos || !opts.val) {
                return argparse_show_usage();
            }
            if (verify(opts.force, "insert a line into the list")) {
                handle_mode_insert(opts.obj, opts.prop, opts.pos, opts.val);
            }
        }
    }
}


